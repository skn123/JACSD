PROGRAM xL0

  USE JACSVD
  USE, INTRINSIC :: ISO_C_BINDING
  IMPLICIT NONE

  INTEGER :: STRAT, LDA, M, N
  INTEGER :: LIWORK, SWP, INFO
  DOUBLE PRECISION :: TOL
  INTEGER, ALLOCATABLE :: IWORK(:)

  CHARACTER(LEN=256) :: F
  INTEGER :: CLK(3), IOU

  DOUBLE PRECISION, ALLOCATABLE :: G(:,:), V(:,:)
  !DIR$ ATTRIBUTES ALIGN: MALIGN_B:: IWORK, G,V
!IBM* ALIGN(MALIGN_B, IWORK, G,V)

  INTEGER :: INFO2(2)
  INTEGER(c_int) :: INFO4(4)
  EQUIVALENCE (INFO2, INFO4)

  CALL READCL(STRAT, M, N, SWP, F, INFO)
  IF (INFO .NE. 0) STOP 'xL0.exe STRAT M N SWP F'

  LDA = M
  IOU = MOD(LDA, D_CL1_LEN)
  IF (IOU .NE. 0) LDA = LDA + (D_CL1_LEN - IOU)

  IOU = GET_IOUNIT('N')
  IF (IOU .LT. 0) STOP 'IOU < 0'

  CALL BIN_OPEN(IOU, TRIM(F), 'OLD', 'READ', INFO)
  IF (INFO .NE. 0) STOP 'BIN_OPEN(.bin)'

  ALLOCATE(G(LDA,N)); G = D_ZERO

  CALL BIN_READ_2D(IOU, LDA, G, M, N, INFO)
  IF (INFO .NE. 0) STOP 'BIN_READ_2D(G)'

  CALL BIN_CLOSE(IOU, INFO)
  IF (INFO .NE. 0) STOP 'BIN_CLOSE(.bin)'

  ALLOCATE(V(LDA,N)); V = D_ZERO

  TOL = D_MONE
  LIWORK = -1
  CALL DJACV0_PREPARE_F(STRAT, LDA, M, N, SWP, TOL, IWORK, LIWORK, INFO)
  PRINT *, 'INFO=', INFO
  IF (INFO .LE. 0) STOP 'L0(workspace query)'
  PRINT *, 'SWP=', SWP
  PRINT *, 'TOL=', TOL
  PRINT *, 'LIWORK=', LIWORK

  INFO = BLAS_PREPARE(.FALSE., .FALSE.)
  IF (INFO .LT. 0) STOP 'BLAS_PREPARE'

  CALL TIMER_START(CLK)
  CALL DJACV0_EXECUTE_F(LDA, M, N, G, V, TOL, SWP, IWORK, INFO2)
  CALL TIMER_STOP(CLK)
  CALL TIMER_PRINT(CLK)

  PRINT *, 'INFO(1)=', INFO4(1)
  PRINT *, 'INFO(2)=', INFO4(2)
  PRINT *, 'INFO(3)=', INFO4(3)
  PRINT *, 'INFO(4)=', INFO4(4)

  INFO = BLAS_FINISH()
  IF (INFO .LT. 0) STOP 'BLAS_FINISH'
  
  IF (ALLOCATED(IWORK)) DEALLOCATE(IWORK)
  DEALLOCATE(V)
  DEALLOCATE(G)

CONTAINS

  SUBROUTINE READCL(STRAT, M, N, SWP, F, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(OUT) :: STRAT, M, N, SWP, INFO
    CHARACTER(LEN=*), INTENT(OUT) :: F

    INFO = COMMAND_ARGUMENT_COUNT()
    IF (INFO .EQ. 5) THEN
       CALL GET_COMMAND_ARGUMENT(1, F)
       READ (F,*) STRAT

       CALL GET_COMMAND_ARGUMENT(2, F)
       READ (F,*) M

       CALL GET_COMMAND_ARGUMENT(3, F)
       READ (F,*) N

       CALL GET_COMMAND_ARGUMENT(4, F)
       READ (F,*) SWP

       CALL GET_COMMAND_ARGUMENT(5, F)
    END IF
    INFO = INFO - 5
  END SUBROUTINE READCL

END PROGRAM xL0
