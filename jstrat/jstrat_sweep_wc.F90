PURE SUBROUTINE JSTRAT_SWEEP_WC(JS, NSTEPS, NPAIRS, JSPAIR, JSCOMM, INFO)
#ifndef JSTRAT_F_MOD
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64
#endif
  IMPLICIT NONE
#ifndef JSTRAT_F_MOD
  INTEGER(KIND=INT64), PARAMETER :: JSMLEX = 8_INT64
  INTERFACE
     PURE SUBROUTINE JSTRAT_NEXT_WC(JS, ARR, INFO) BIND(C,NAME='jstrat_next_f')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64
       IMPLICIT NONE
       INTEGER(KIND=INT64), INTENT(INOUT), TARGET :: JS(*)
       INTEGER(KIND=INT64), INTENT(OUT), TARGET :: ARR(2,2,*), INFO
     END SUBROUTINE JSTRAT_NEXT_WC
  END INTERFACE
  INTERFACE
     PURE SUBROUTINE JSTRAT_UNPACK_WC(JS, ARR, PAIR, COMM, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT32, INT64
       IMPLICIT NONE
       INTEGER(KIND=INT64), PARAMETER :: JSMLEN = 6_INT64
       INTEGER(KIND=INT64), INTENT(IN) :: JS(JSMLEN), ARR(2,2,*)
       INTEGER(KIND=INT64), INTENT(OUT) :: PAIR(2,*), COMM(2,*)
       INTEGER(KIND=INT64), INTENT(INOUT) :: INFO
     END SUBROUTINE JSTRAT_UNPACK_WC
  END INTERFACE
#endif
  INTEGER(KIND=INT64), INTENT(INOUT) :: JS(JSMLEX)
  INTEGER(KIND=INT64), INTENT(IN) :: NSTEPS, NPAIRS
  INTEGER(KIND=INT64), INTENT(OUT) :: JSPAIR(2,NPAIRS,NSTEPS), JSCOMM(2,NPAIRS,NSTEPS), INFO

  INTEGER(KIND=INT64), ALLOCATABLE :: JSARR(:,:,:)
  INTEGER(KIND=INT64) :: I

  INFO = 0
  IF (NSTEPS .LT. 0) INFO = -2
  IF (NPAIRS .LT. 0) INFO = -3
  IF (INFO .NE. 0) RETURN

  ALLOCATE(JSARR(2,2,NPAIRS),STAT=I)
  IF (I .GT. 0) THEN
     INFO = -1
     GOTO 1
  END IF

  JS(JSMLEX-1) = NSTEPS
  JS(JSMLEX) = NPAIRS
  DO I = 1, NSTEPS
     CALL JSTRAT_NEXT_WC(JS, JSARR, INFO)
     CALL JSTRAT_UNPACK_WC(JS, JSARR, JSPAIR(1,1,I), JSCOMM(1,1,I), INFO)
     IF (INFO .NE. NPAIRS) THEN
        INFO = I
        GOTO 1
     END IF
  END DO

  INFO = 0
1 IF (ALLOCATED(JSARR)) DEALLOCATE(JSARR)
END SUBROUTINE JSTRAT_SWEEP_WC
