MODULE JSTRAT_F
  USE, INTRINSIC :: ISO_C_BINDING
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT32, INT64
  IMPLICIT NONE

  INTEGER(KIND=INT64), PARAMETER :: JSMLEN = 6_INT64
  INTEGER(KIND=INT64), PARAMETER :: JSMLEX = 8_INT64
  INTEGER(KIND=INT64), PARAMETER :: JSROWC = 0_INT64
  INTEGER(KIND=INT64), PARAMETER :: JSCOLC = 1_INT64
  INTEGER(KIND=INT64), PARAMETER :: JSMENC = 2_INT64
  INTEGER(KIND=INT64), PARAMETER :: JSMEWC = 3_INT64
  INTEGER(KIND=INT64), PARAMETER :: JSMMNC = 4_INT64
  INTEGER(KIND=INT64), PARAMETER :: JSMMWC = 5_INT64

  INTERFACE
     PURE SUBROUTINE JSTRAT_INIT(JS, ID, N, INFO) BIND(C,NAME='jstrat_init_f')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64
       IMPLICIT NONE
       INTEGER(KIND=INT64), INTENT(IN), TARGET :: ID, N
       INTEGER(KIND=INT64), INTENT(OUT), TARGET :: JS(*), INFO
     END SUBROUTINE JSTRAT_INIT
  END INTERFACE

  INTERFACE
     PURE SUBROUTINE JSTRAT_NEXT_NC(JS, ARR, INFO) BIND(C,NAME='jstrat_next_f')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64
       IMPLICIT NONE
       INTEGER(KIND=INT64), INTENT(INOUT), TARGET :: JS(*)
       INTEGER(KIND=INT64), INTENT(OUT), TARGET :: ARR(2,*), INFO
     END SUBROUTINE JSTRAT_NEXT_NC
  END INTERFACE

  INTERFACE
     PURE SUBROUTINE JSTRAT_NEXT_WC(JS, ARR, INFO) BIND(C,NAME='jstrat_next_f')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64
       IMPLICIT NONE
       INTEGER(KIND=INT64), INTENT(INOUT), TARGET :: JS(*)
       INTEGER(KIND=INT64), INTENT(OUT), TARGET :: ARR(2,2,*), INFO
     END SUBROUTINE JSTRAT_NEXT_WC
  END INTERFACE

  INTERFACE
     PURE SUBROUTINE JSTRAT_FREE(JS) BIND(C,NAME='jstrat_free_f')
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: INT64
       IMPLICIT NONE
       INTEGER(KIND=INT64), INTENT(INOUT), TARGET :: JS(*)
     END SUBROUTINE JSTRAT_FREE
  END INTERFACE

CONTAINS

  PURE SUBROUTINE JSTRAT_UNPACK_NC(JS, ARR, PAIR, INFO)
    IMPLICIT NONE

    INTEGER(KIND=INT64), INTENT(IN) :: JS(JSMLEN), ARR(2,*)
    INTEGER(KIND=INT64), INTENT(OUT) :: PAIR(2,*)
    INTEGER(KIND=INT64), INTENT(INOUT) :: INFO

    INTEGER(KIND=INT64) :: J, L
    INTEGER(KIND=INT32) :: C(2)
    EQUIVALENCE (L,C)

    SELECT CASE (JS(1))
    CASE (JSROWC, JSCOLC)
       IF (INFO .NE. 1) THEN
          INFO = -2
       ELSE
          PAIR(1,1) = ARR(1,1) + 1
          PAIR(2,1) = ARR(2,1) + 1
       END IF
    CASE (JSMENC)
       IF (INFO .NE. (JS(2) / 2)) THEN
          INFO = -2
       ELSE
          DO J = 1, INFO
             PAIR(1,J) = ARR(1,J) + 1
             PAIR(2,J) = ARR(2,J) + 1
          END DO
       END IF
    CASE (JSMMNC)
       IF (INFO .NE. -(JS(2) / 2)) THEN
          INFO = -2
       ELSE
          INFO = -INFO
          DO J = 1, INFO
             L = ARR(1,J)
             PAIR(1,J) = C(1) + 1
             L = ARR(2,J)
             PAIR(2,J) = C(1) + 1
          END DO
       END IF
    CASE DEFAULT
       INFO = -1
    END SELECT
  END SUBROUTINE JSTRAT_UNPACK_NC

  PURE SUBROUTINE JSTRAT_UNPACK_WC(JS, ARR, PAIR, COMM, INFO)
    IMPLICIT NONE

    INTEGER(KIND=INT64), INTENT(IN) :: JS(JSMLEN), ARR(2,2,*)
    INTEGER(KIND=INT64), INTENT(OUT) :: PAIR(2,*), COMM(2,*)
    INTEGER(KIND=INT64), INTENT(INOUT) :: INFO

    INTEGER(KIND=INT64) :: K, L
    INTEGER(KIND=INT32) :: C(2)
    EQUIVALENCE (L,C)

    SELECT CASE (JS(1))
    CASE (JSMEWC)
       IF (INFO .NE. (JS(2) / 2)) THEN
          INFO = -2
       ELSE
          DO K = 1, INFO
             PAIR(1,K) = ARR(1,1,K) + 1
             PAIR(2,K) = ARR(2,1,K) + 1
             COMM(1,K) = ARR(1,2,K)
             COMM(2,K) = ARR(2,2,K)
          END DO
       END IF
    CASE (JSMMWC)
       IF (INFO .NE. -(JS(2) / 2)) THEN
          INFO = -2
       ELSE
          INFO = -INFO
          DO K = 1, INFO
             L = ARR(1,1,K)
             PAIR(1,K) = C(1) + 1
             L = ARR(2,1,K)
             PAIR(2,K) = C(1) + 1
             L = ARR(1,2,K)
             COMM(1,K) = C(1)
             L = ARR(2,2,K)
             COMM(2,K) = C(1)
          END DO
       END IF
    CASE DEFAULT
       INFO = -1
    END SELECT
  END SUBROUTINE JSTRAT_UNPACK_WC

  PURE SUBROUTINE JSTRAT_SWEEP_NC(JS, NSTEPS, NPAIRS, JSPAIR, INFO)
    IMPLICIT NONE
    INTEGER(KIND=INT64), INTENT(INOUT) :: JS(JSMLEX)
    INTEGER(KIND=INT64), INTENT(IN) :: NSTEPS, NPAIRS
    INTEGER(KIND=INT64), INTENT(OUT) :: JSPAIR(2,NPAIRS,NSTEPS), INFO

    INTEGER(KIND=INT64), ALLOCATABLE :: JSARR(:,:)
    INTEGER(KIND=INT64) :: I

    INFO = 0
    IF (NSTEPS .LT. 0) INFO = -2
    IF (NPAIRS .LT. 0) INFO = -3
    IF (INFO .NE. 0) RETURN

    ALLOCATE(JSARR(2,NPAIRS),STAT=I)
    IF (I .GT. 0) THEN
       INFO = -1
       GOTO 1
    END IF

    JS(JSMLEX-1) = NSTEPS
    JS(JSMLEX) = NPAIRS
    DO I = 1, NSTEPS
       CALL JSTRAT_NEXT_NC(JS, JSARR, INFO)
       CALL JSTRAT_UNPACK_NC(JS, JSARR, JSPAIR(1,1,I), INFO)
       IF (INFO .NE. NPAIRS) THEN
          INFO = I
          GOTO 1
       END IF
    END DO

    INFO = 0
1   IF (ALLOCATED(JSARR)) DEALLOCATE(JSARR)
  END SUBROUTINE JSTRAT_SWEEP_NC

  PURE SUBROUTINE JSTRAT_SWEEP_WC(JS, NSTEPS, NPAIRS, JSPAIR, JSCOMM, INFO)
    IMPLICIT NONE
    INTEGER(KIND=INT64), INTENT(INOUT) :: JS(JSMLEX)
    INTEGER(KIND=INT64), INTENT(IN) :: NSTEPS, NPAIRS
    INTEGER(KIND=INT64), INTENT(OUT) :: JSPAIR(2,NPAIRS,NSTEPS), JSCOMM(2,NPAIRS,NSTEPS), INFO

    INTEGER(KIND=INT64), ALLOCATABLE :: JSARR(:,:,:)
    INTEGER(KIND=INT64) :: I

    INFO = 0
    IF (NSTEPS .LT. 0) INFO = -2
    IF (NPAIRS .LT. 0) INFO = -3
    IF (INFO .NE. 0) RETURN

    ALLOCATE(JSARR(2,2,NPAIRS),STAT=I)
    IF (I .GT. 0) THEN
       INFO = -1
       GOTO 2
    END IF

    JS(JSMLEX-1) = NSTEPS
    JS(JSMLEX) = NPAIRS
    DO I = 1, NSTEPS
       CALL JSTRAT_NEXT_WC(JS, JSARR, INFO)
       CALL JSTRAT_UNPACK_WC(JS, JSARR, JSPAIR(1,1,I), JSCOMM(1,1,I), INFO)
       IF (INFO .NE. NPAIRS) THEN
          INFO = I
          GOTO 2
       END IF
    END DO

    INFO = 0
2   IF (ALLOCATED(JSARR)) DEALLOCATE(JSARR)
  END SUBROUTINE JSTRAT_SWEEP_WC

END MODULE JSTRAT_F
