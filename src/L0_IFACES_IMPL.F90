SUBROUTINE DJACV0_PARAMQ_F(STRAT, LDA, M, N, SWP, TOL, JS, LIWORK, INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: STRAT, LDA, M, N, SWP
  REAL(DWP), INTENT(OUT) :: TOL
  INTEGER, INTENT(OUT) :: JS(JSMLEN), LIWORK, INFO

  INTEGER :: R
  !DIR$ ASSUME_ALIGNED JS:MALIGN_B

  INFO = 0

  IF (STRAT .LT. 0) THEN
     INFO = -1
     RETURN
  END IF
  IF ((LDA .LE. 0) .OR. (MOD(LDA,D_VEC_LEN) .NE. 0)) THEN
     INFO = -2
     RETURN
  END IF
  IF ((M .LE. 0) .OR. (M .GT. LDA) .OR. (M .GT. N) .OR. (MOD(M,D_VEC_LEN) .NE. 0)) THEN
     INFO = -3
     RETURN
  END IF
  IF ((N .LE. 0) .OR. (N .GT. LDA) .OR. (N .GT. M) .OR. (MOD(N,2*D_VEC_LEN) .NE. 0)) THEN
     INFO = -4
     RETURN
  END IF
  IF (SWP .LE. 0) THEN
     INFO = -5
     RETURN
  END IF

  ! TOL = M * SCALE(EPSILON(D_ONE),-1)
  TOL = SQRT(REAL(M,DWP)) * SCALE(EPSILON(D_ONE),-1)

  CALL JSTRAT_INIT(JS, STRAT, N, INFO)
  IF ((INFO .LT. (N-1)) .OR. (INFO .GT. N)) THEN
     INFO = -1
     RETURN
  END IF

  LIWORK = N * (N / 2)
  R = MOD(LIWORK, I_VEC_LEN)
  IF (R .NE. 0) LIWORK = LIWORK + (I_VEC_LEN - R)
  LIWORK = 2 * LIWORK
END SUBROUTINE DJACV0_PARAMQ_F

SUBROUTINE DJACV0_PREPARE_F(STEPS, LDA, M, N, JS, IWORK, LIWORK, INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: STEPS, LDA, M, N, LIWORK
  INTEGER, INTENT(INOUT) :: JS(JSMLEN)
  INTEGER, INTENT(OUT) :: IWORK(LIWORK), INFO

  INTEGER :: PIV(2,MAX(1,N/2))
  !DIR$ ATTRIBUTES ALIGN: MALIGN_B:: PIV
!IBM* ALIGN(MALIGN_B, PIV)

  INTEGER :: N_2, MINWRK, R, I, J
  
  !DIR$ ASSUME_ALIGNED JS:MALIGN_B,IWORK:MALIGN_B
  !DIR$ ASSUME (MOD(LDA,D_VEC_LEN) .EQ. 0)

  INFO = 0

  IF ((STEPS .LT. (N-1)) .OR. (STEPS .GT. N)) THEN
     INFO = -1
     RETURN
  END IF
  IF ((LDA .LE. 0) .OR. (MOD(LDA,D_VEC_LEN) .NE. 0)) THEN
     INFO = -2
     RETURN
  END IF
  IF ((M .LE. 0) .OR. (M .GT. LDA) .OR. (M .GT. N) .OR. (MOD(M,D_VEC_LEN) .NE. 0)) THEN
     INFO = -3
     RETURN
  END IF
  IF ((N .LE. 0) .OR. (N .GT. LDA) .OR. (N .GT. M) .OR. (MOD(N,2*D_VEC_LEN) .NE. 0)) THEN
     INFO = -4
     RETURN
  END IF

  N_2 = N / 2
  MINWRK = N * N_2
  R = MOD(MINWRK, I_VEC_LEN)
  IF (R .NE. 0) MINWRK = MINWRK + (I_VEC_LEN - R)
  R = 2 * MINWRK
  IF (LIWORK .LT. R) THEN
     INFO = -7
     RETURN
  END IF

  R = 1
  DO I = 1, STEPS
     CALL JSTRAT_NEXT(JS, PIV, INFO)
     IF (INFO .NE. N_2) THEN
        INFO = -5
        RETURN
     END IF
     DO J = 1, N_2
        IWORK(R) = PIV(1,J) * LDA ! P
        IWORK(MINWRK + R) = PIV(2,J) * LDA ! Q
        R = R + 1
     END DO
  END DO
  CALL JSTRAT_FREE(JS)

  ! Fill the rest with some errorneous values.
  J = -1
  DO I = R, MINWRK
     IWORK(R) = J
     IWORK(MINWRK + R) = J
     J = J - 1
  END DO
END SUBROUTINE DJACV0_PREPARE_F

SUBROUTINE DJACV0_EXECUTE_F(LDA, M, N, G, V, TOL, SWP, STEPS, IWORK, INFO)
  USE, INTRINSIC :: ISO_C_BINDING
  IMPLICIT NONE

#ifdef USE_X200
  INTERFACE
     INTEGER(8) FUNCTION DJACV0_EXECUTE_C(M, N, G, V, TOL, SWP, IWORK, INFO) BIND(C,name='avx512_djacv0_execute')
       INTEGER(4), VALUE :: M, N, SWP
       REAL(8), INTENT(INOUT), TARGET :: G(*), V(*)
       REAL(8), VALUE :: TOL
       INTEGER(8), INTENT(IN), TARGET :: IWORK(*)
       INTEGER(4), INTENT(OUT), TARGET :: INFO
     END FUNCTION DJACV0_EXECUTE_C
  END INTERFACE
#else
  INTERFACE
     INTEGER(8) FUNCTION DJACV0_EXECUTE_C(M, N, G, V, TOL, SWP, IWORK, INFO) BIND(C,name='avx2_fma_djacv0_execute')
       INTEGER(4), VALUE :: M, N, SWP
       REAL(8), INTENT(INOUT), TARGET :: G(*), V(*)
       REAL(8), VALUE :: TOL
       INTEGER(8), INTENT(IN), TARGET :: IWORK(*)
       INTEGER(4), INTENT(OUT), TARGET :: INFO
     END FUNCTION DJACV0_EXECUTE_C
  END INTERFACE
#endif

  INTEGER, INTENT(IN) :: LDA, M, N, SWP, STEPS, IWORK(*)
  REAL(DWP), INTENT(IN) :: TOL
  REAL(DWP), INTENT(INOUT) :: G(LDA,N)
  REAL(DWP), INTENT(OUT) :: V(LDA,N)
  INTEGER, INTENT(OUT) :: INFO(2)
  EXTERNAL :: DLASET

  INTEGER :: JNFO
  INTEGER(c_int) :: JNFO2(2)
  EQUIVALENCE (JNFO, JNFO2)

  !DIR$ ASSUME_ALIGNED G:MALIGN_B,V:MALIGN_B,IWORK:MALIGN_B
  !DIR$ ASSUME (MOD(LDA,D_VEC_LEN) .EQ. 0)

  CALL DLASET('A', N, N, D_ZERO, D_ONE, V, LDA)

  JNFO2(2) = INT(SWP,c_int)
  IF (STEPS .EQ. N) JNFO2(2) = -JNFO2(2)

  INFO(1) = INT(DJACV0_EXECUTE_C(INT(M,c_int), INT(N,c_int), G, V, TOL, JNFO2(2), IWORK, JNFO2(1)))
  INFO(2) = JNFO
END SUBROUTINE DJACV0_EXECUTE_F
