PROGRAM DGESVJ_TEST
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: c_long_long
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, REAL128
  USE BIO
  IMPLICIT NONE
  DOUBLE PRECISION, PARAMETER :: ZERO = 0.0D0, ONE = 1.0D0
  CHARACTER(LEN=256) :: CLA
  DOUBLE PRECISION, ALLOCATABLE :: G(:,:), V(:,:), SVA(:), WORK(:)
  REAL(KIND=REAL128), ALLOCATABLE :: QS(:)
  REAL(KIND=REAL128) :: Q
  INTEGER(KIND=c_long_long) :: T
  INTEGER :: I, M, N, LDG, LDV, MV, LWORK, INFO
  INTEGER(KIND=c_long_long), EXTERNAL :: PVN_TIME_MONO_TICKS, PVN_TIME_MONO_FREQ
  EXTERNAL :: DGESVJ
  INFO = COMMAND_ARGUMENT_COUNT()
  IF (INFO .NE. 3) STOP 'dgesvj_test.exe M N FN'
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  READ (CLA,*) M
  IF (M .LE. 0) STOP 'M <= 0'
  CALL GET_COMMAND_ARGUMENT(2, CLA)
  READ (CLA,*) N
  IF (N .LE. 0) STOP 'N <= 0'
  IF (N .GT. M) STOP 'N > M'
  CALL GET_COMMAND_ARGUMENT(3, CLA)
  IF (LEN_TRIM(CLA) .LE. 0) STOP 'FN is a blank string'
  LDG = M
  ALLOCATE(G(LDG,N))
  G = ZERO
  CALL BIO_OPEN(I, TRIM(CLA)//'.G', 'RO', INFO)
  IF (INFO .NE. 0) STOP 'BIO_OPEN(G)'
  CALL BIO_READ_D2(I, LDG, N, G, INFO)
  IF (INFO .NE. 0) STOP 'BIO_READ_D2(G)'
  CALL BIO_CLOSE(I, INFO)
  IF (INFO .NE. 0) STOP 'BIO_CLOSE(G)'
  LDV = N
  ALLOCATE(V(LDV,N))
  V = ZERO
  MV = N
  ALLOCATE(SVA(N))
  SVA = ZERO
  LWORK = MAX(6, M+N)
  ALLOCATE(WORK(LWORK))
  INFO = 0
  T = PVN_TIME_MONO_TICKS()
  CALL DGESVJ('G', 'U', 'V', M, N, G, LDG, SVA, MV, V, LDV, WORK, LWORK, INFO)
  T = PVN_TIME_MONO_TICKS() - T
  Q = PVN_TIME_MONO_FREQ()
  WRITE (OUTPUT_UNIT,'(3A,2(I5,A),I4,A,F15.6,A,ES25.17E3,A,2(I5,A),I3,A,ES25.17E3,A,ES25.17E3)')&
       '"',TRIM(CLA),'",', M,',', N,',', INFO,',', (T / Q),',',&
       WORK(1),',', INT(WORK(2)),',', INT(WORK(3)),',', INT(WORK(4)),',', WORK(5),',', WORK(6)
  FLUSH(OUTPUT_UNIT)
  ALLOCATE(QS(N))
  IF (WORK(1) .NE. ONE) THEN
     Q = WORK(1)
     DO I = 1, N
        QS(I) = SVA(I) * Q
     END DO
  ELSE ! WORK(1) = 1
     DO I = 1, N
        QS(I) = SVA(I)
     END DO
  END IF
  CALL BIO_OPEN(I, TRIM(CLA)//'.SL', 'WO', INFO)
  IF (INFO .NE. 0) STOP 'BIO_OPEN(SL)'
  CALL BIO_WRITE_Q1(I, N, QS, INFO)
  IF (INFO .NE. 0) STOP 'BIO_WRITE_Q1(QS)'
  CALL BIO_CLOSE(I, INFO)
  IF (INFO .NE. 0) STOP 'BIO_CLOSE(SL)'
  DEALLOCATE(QS)
  DEALLOCATE(WORK)
  DEALLOCATE(SVA)
  CALL BIO_OPEN(I, TRIM(CLA)//'.VL', 'WO', INFO)
  IF (INFO .NE. 0) STOP 'BIO_OPEN(VL)'
  CALL BIO_WRITE_D2(I, LDV, N, V, INFO)
  IF (INFO .NE. 0) STOP 'BIO_WRITE_D2(V)'
  CALL BIO_CLOSE(I, INFO)
  IF (INFO .NE. 0) STOP 'BIO_CLOSE(VL)'
  DEALLOCATE(V)
  CALL BIO_OPEN(I, TRIM(CLA)//'.UL', 'WO', INFO)
  IF (INFO .NE. 0) STOP 'BIO_OPEN(UL)'
  CALL BIO_WRITE_D2(I, LDG, N, G, INFO)
  IF (INFO .NE. 0) STOP 'BIO_WRITE_D2(G)'
  CALL BIO_CLOSE(I, INFO)
  IF (INFO .NE. 0) STOP 'BIO_CLOSE(UL)'
  DEALLOCATE(G)
END PROGRAM DGESVJ_TEST
