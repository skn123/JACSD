MODULE VN_IMAGINARY_F
  IMPLICIT NONE

  INTEGER, PARAMETER, PRIVATE :: SWP = KIND(0.0E0)
  INTEGER, PARAMETER, PRIVATE :: DWP = KIND(0.0D0)

  REAL(KIND=SWP), PARAMETER, PRIVATE :: S_ZERO = 0.0_SWP
  REAL(KIND=DWP), PARAMETER, PRIVATE :: D_ZERO = 0.0_DWP

  TYPE, BIND(C) :: CIMAGINARY
     REAL(KIND=SWP) :: J
  END TYPE CIMAGINARY
  TYPE, BIND(C) :: ZIMAGINARY
     REAL(KIND=DWP) :: J
  END TYPE ZIMAGINARY

  ! unary operators
  INTERFACE OPERATOR(+)
     MODULE PROCEDURE CPLSJ,ZPLSJ
  END INTERFACE OPERATOR(+)
  INTERFACE OPERATOR(-)
     MODULE PROCEDURE CNEGJ,ZNEGJ
  END INTERFACE OPERATOR(-)

  ! binary arithmetic operators
  INTERFACE OPERATOR(+)
     MODULE PROCEDURE CJPLUSJ,ZJPLUSJ
     MODULE PROCEDURE CJPLUSI,ZJPLUSI
     MODULE PROCEDURE CIPLUSJ,ZIPLUSJ
     MODULE PROCEDURE CJPLUSR,ZJPLUSR
     MODULE PROCEDURE CRPLUSJ,ZRPLUSJ
     MODULE PROCEDURE CJPLUSC,ZJPLUSC
     MODULE PROCEDURE CCPLUSJ,ZCPLUSJ
  END INTERFACE OPERATOR(+)
  INTERFACE OPERATOR(-)
     MODULE PROCEDURE CJMINUSJ,ZJMINUSJ
     MODULE PROCEDURE CJMINUSI,ZJMINUSI
     MODULE PROCEDURE CIMINUSJ,ZIMINUSJ
     MODULE PROCEDURE CJMINUSR,ZJMINUSR
     MODULE PROCEDURE CRMINUSJ,ZRMINUSJ
     MODULE PROCEDURE CJMINUSC,ZJMINUSC
     MODULE PROCEDURE CCMINUSJ,ZCMINUSJ
  END INTERFACE OPERATOR(-)
  INTERFACE OPERATOR(*)
     MODULE PROCEDURE CJTIMESJ,ZJTIMESJ
     MODULE PROCEDURE CJTIMESI,ZJTIMESI
     MODULE PROCEDURE CITIMESJ,ZITIMESJ
     MODULE PROCEDURE CJTIMESR,ZJTIMESR
     MODULE PROCEDURE CRTIMESJ,ZRTIMESJ
     MODULE PROCEDURE CJTIMESC,ZJTIMESC
     MODULE PROCEDURE CCTIMESJ,ZCTIMESJ
  END INTERFACE OPERATOR(*)
  INTERFACE OPERATOR(/)
     MODULE PROCEDURE CJDIVJ,ZJDIVJ
     MODULE PROCEDURE CJDIVI,ZJDIVI
     MODULE PROCEDURE CIDIVJ,ZIDIVJ
     MODULE PROCEDURE CJDIVR,ZJDIVR
     MODULE PROCEDURE CRDIVJ,ZRDIVJ
     MODULE PROCEDURE CJDIVC,ZJDIVC
     MODULE PROCEDURE CCDIVJ,ZCDIVJ
  END INTERFACE OPERATOR(/)

  ! relational operators
  INTERFACE OPERATOR(.EQ.)
     MODULE PROCEDURE CJEQJ,ZJEQJ
     MODULE PROCEDURE CJEQI,ZJEQI
     MODULE PROCEDURE CIEQJ,ZIEQJ
     MODULE PROCEDURE CJEQR,ZJEQR
     MODULE PROCEDURE CREQJ,ZREQJ
     MODULE PROCEDURE CJEQC,ZJEQC
     MODULE PROCEDURE CCEQJ,ZCEQJ
  END INTERFACE OPERATOR(.EQ.)
  INTERFACE OPERATOR(.NE.)
     MODULE PROCEDURE CJNEJ,ZJNEJ
     MODULE PROCEDURE CJNEI,ZJNEI
     MODULE PROCEDURE CINEJ,ZINEJ
     MODULE PROCEDURE CJNER,ZJNER
     MODULE PROCEDURE CRNEJ,ZRNEJ
     MODULE PROCEDURE CJNEC,ZJNEC
     MODULE PROCEDURE CCNEJ,ZCNEJ
  END INTERFACE OPERATOR(.NE.)
  INTERFACE OPERATOR(.LT.)
     MODULE PROCEDURE CJLTJ,ZJLTJ
  END INTERFACE OPERATOR(.LT.)
  INTERFACE OPERATOR(.LE.)
     MODULE PROCEDURE CJLEJ,ZJLEJ
  END INTERFACE OPERATOR(.LE.)
  INTERFACE OPERATOR(.GE.)
     MODULE PROCEDURE CJGEJ,ZJGEJ
  END INTERFACE OPERATOR(.GE.)
  INTERFACE OPERATOR(.GT.)
     MODULE PROCEDURE CJGTJ,ZJGTJ
  END INTERFACE OPERATOR(.GT.)

  ! component extractors
  INTERFACE REAL
     MODULE PROCEDURE CJREAL,ZJREAL
  END INTERFACE REAL
  INTERFACE AIMAG
     MODULE PROCEDURE CJIMAG,ZJIMAG
  END INTERFACE AIMAG

CONTAINS

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  ELEMENTAL FUNCTION CPLSJ(A)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    TYPE(CIMAGINARY) :: CPLSJ
    CPLSJ%J = A%J
  END FUNCTION CPLSJ
  ELEMENTAL FUNCTION ZPLSJ(A)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    TYPE(ZIMAGINARY) :: ZPLSJ
    ZPLSJ%J = A%J
  END FUNCTION ZPLSJ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  ELEMENTAL FUNCTION CNEGJ(A)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    TYPE(CIMAGINARY) :: CNEGJ
    CNEGJ%J = -A%J
  END FUNCTION CNEGJ
  ELEMENTAL FUNCTION ZNEGJ(A)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    TYPE(ZIMAGINARY) :: ZNEGJ
    ZNEGJ%J = -A%J
  END FUNCTION ZNEGJ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  PURE FUNCTION CJPLUSJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    TYPE(CIMAGINARY) :: CJPLUSJ
    CJPLUSJ%J = A%J + B%J
  END FUNCTION CJPLUSJ
  PURE FUNCTION ZJPLUSJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    TYPE(ZIMAGINARY) :: ZJPLUSJ
    ZJPLUSJ%J = A%J + B%J
  END FUNCTION ZJPLUSJ

  PURE FUNCTION CJPLUSI(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJPLUSI
    CJPLUSI = CMPLX(REAL(B,SWP), A%J, SWP)
  END FUNCTION CJPLUSI
  PURE FUNCTION ZJPLUSI(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJPLUSI
    ZJPLUSI = CMPLX(REAL(B,DWP), A%J, DWP)
  END FUNCTION ZJPLUSI

  PURE FUNCTION CIPLUSJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CIPLUSJ
    CIPLUSJ = CMPLX(REAL(A,SWP), B%J, SWP)
  END FUNCTION CIPLUSJ
  PURE FUNCTION ZIPLUSJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZIPLUSJ
    ZIPLUSJ = CMPLX(REAL(A,DWP), B%J, DWP)
  END FUNCTION ZIPLUSJ

  PURE FUNCTION CJPLUSR(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJPLUSR
    CJPLUSR = CMPLX(B, A%J, SWP)
  END FUNCTION CJPLUSR
  PURE FUNCTION ZJPLUSR(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJPLUSR
    ZJPLUSR = CMPLX(B, A%J, DWP)
  END FUNCTION ZJPLUSR

  PURE FUNCTION CRPLUSJ(A,B)
    IMPLICIT NONE
    REAL(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CRPLUSJ
    CRPLUSJ = CMPLX(A, B%J, SWP)
  END FUNCTION CRPLUSJ
  PURE FUNCTION ZRPLUSJ(A,B)
    IMPLICIT NONE
    REAL(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZRPLUSJ
    ZRPLUSJ = CMPLX(A, B%J, DWP)
  END FUNCTION ZRPLUSJ

  PURE FUNCTION CJPLUSC(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=SWP), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJPLUSC
    CJPLUSC = CMPLX(REAL(B), A%J + AIMAG(B), SWP)
  END FUNCTION CJPLUSC
  PURE FUNCTION ZJPLUSC(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=DWP), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJPLUSC
    ZJPLUSC = CMPLX(REAL(B), A%J + AIMAG(B), DWP)
  END FUNCTION ZJPLUSC

  PURE FUNCTION CCPLUSJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CCPLUSJ
    CCPLUSJ = CMPLX(REAL(A), AIMAG(A) + B%J, SWP)
  END FUNCTION CCPLUSJ
  PURE FUNCTION ZCPLUSJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZCPLUSJ
    ZCPLUSJ = CMPLX(REAL(A), AIMAG(A) + B%J, DWP)
  END FUNCTION ZCPLUSJ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  PURE FUNCTION CJMINUSJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    TYPE(CIMAGINARY) :: CJMINUSJ
    CJMINUSJ%J = A%J - B%J
  END FUNCTION CJMINUSJ
  PURE FUNCTION ZJMINUSJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    TYPE(ZIMAGINARY) :: ZJMINUSJ
    ZJMINUSJ%J = A%J - B%J
  END FUNCTION ZJMINUSJ

  PURE FUNCTION CJMINUSI(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJMINUSI
    CJMINUSI = CMPLX(-REAL(B,SWP), A%J, SWP)
  END FUNCTION CJMINUSI
  PURE FUNCTION ZJMINUSI(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJMINUSI
    ZJMINUSI = CMPLX(-REAL(B,DWP), A%J, DWP)
  END FUNCTION ZJMINUSI

  PURE FUNCTION CIMINUSJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CIMINUSJ
    CIMINUSJ = CMPLX(REAL(A,SWP), -B%J, SWP)
  END FUNCTION CIMINUSJ
  PURE FUNCTION ZIMINUSJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZIMINUSJ
    ZIMINUSJ = CMPLX(REAL(A,DWP), -B%J, DWP)
  END FUNCTION ZIMINUSJ

  PURE FUNCTION CJMINUSR(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJMINUSR
    CJMINUSR = CMPLX(-B, A%J, SWP)
  END FUNCTION CJMINUSR
  PURE FUNCTION ZJMINUSR(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJMINUSR
    ZJMINUSR = CMPLX(-B, A%J, DWP)
  END FUNCTION ZJMINUSR

  PURE FUNCTION CRMINUSJ(A,B)
    IMPLICIT NONE
    REAL(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CRMINUSJ
    CRMINUSJ = CMPLX(A, -B%J, SWP)
  END FUNCTION CRMINUSJ
  PURE FUNCTION ZRMINUSJ(A,B)
    IMPLICIT NONE
    REAL(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZRMINUSJ
    ZRMINUSJ = CMPLX(A, -B%J, DWP)
  END FUNCTION ZRMINUSJ

  PURE FUNCTION CJMINUSC(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=SWP), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJMINUSC
    CJMINUSC = CMPLX(-REAL(B), A%J - AIMAG(B), SWP)
  END FUNCTION CJMINUSC
  PURE FUNCTION ZJMINUSC(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=DWP), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJMINUSC
    ZJMINUSC = CMPLX(-REAL(B), A%J - AIMAG(B), DWP)
  END FUNCTION ZJMINUSC

  PURE FUNCTION CCMINUSJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CCMINUSJ
    CCMINUSJ = CMPLX(REAL(A), AIMAG(A) - B%J, SWP)
  END FUNCTION CCMINUSJ
  PURE FUNCTION ZCMINUSJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZCMINUSJ
    ZCMINUSJ = CMPLX(REAL(A), AIMAG(A) - B%J, DWP)
  END FUNCTION ZCMINUSJ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  PURE FUNCTION CJTIMESJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    REAL(KIND=SWP) :: CJTIMESJ
    CJTIMESJ = -(A%J * B%J)
  END FUNCTION CJTIMESJ
  PURE FUNCTION ZJTIMESJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    REAL(KIND=DWP) :: ZJTIMESJ
    ZJTIMESJ = -(A%J * B%J)
  END FUNCTION ZJTIMESJ

  PURE FUNCTION CJTIMESI(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CJTIMESI
    CJTIMESI%J = A%J * B
  END FUNCTION CJTIMESI
  PURE FUNCTION ZJTIMESI(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZJTIMESI
    ZJTIMESI%J = A%J * B
  END FUNCTION ZJTIMESI

  PURE FUNCTION CITIMESJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CITIMESJ
    CITIMESJ%J = A * B%J
  END FUNCTION CITIMESJ
  PURE FUNCTION ZITIMESJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZITIMESJ
    ZITIMESJ%J = A * B%J
  END FUNCTION ZITIMESJ

  PURE FUNCTION CJTIMESR(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP), INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CJTIMESR
    CJTIMESR%J = A%J * B
  END FUNCTION CJTIMESR
  PURE FUNCTION ZJTIMESR(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP), INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZJTIMESR
    ZJTIMESR%J = A%J * B
  END FUNCTION ZJTIMESR

  PURE FUNCTION CRTIMESJ(A,B)
    IMPLICIT NONE
    REAL(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CRTIMESJ
    CRTIMESJ%J = A * B%J
  END FUNCTION CRTIMESJ
  PURE FUNCTION ZRTIMESJ(A,B)
    IMPLICIT NONE
    REAL(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZRTIMESJ
    ZRTIMESJ%J = A * B%J
  END FUNCTION ZRTIMESJ

  PURE FUNCTION CJTIMESC(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=SWP), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJTIMESC
    CJTIMESC = CMPLX(-(A%J * AIMAG(B)), A%J * REAL(B), SWP)
  END FUNCTION CJTIMESC
  PURE FUNCTION ZJTIMESC(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=DWP), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJTIMESC
    ZJTIMESC = CMPLX(-(A%J * AIMAG(B)), A%J * REAL(B), DWP)
  END FUNCTION ZJTIMESC

  PURE FUNCTION CCTIMESJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CCTIMESJ
    CCTIMESJ = CMPLX(-(AIMAG(A) * B%J), REAL(A) * B%J, SWP)
  END FUNCTION CCTIMESJ
  PURE FUNCTION ZCTIMESJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZCTIMESJ
    ZCTIMESJ = CMPLX(-(AIMAG(A) * B%J), REAL(A) * B%J, DWP)
  END FUNCTION ZCTIMESJ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  PURE FUNCTION CJDIVJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    REAL(KIND=SWP) :: CJDIVJ
    CJDIVJ = A%J / B%J
  END FUNCTION CJDIVJ
  PURE FUNCTION ZJDIVJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    REAL(KIND=DWP) :: ZJDIVJ
    ZJDIVJ = A%J / B%J
  END FUNCTION ZJDIVJ

  PURE FUNCTION CJDIVI(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CJDIVI
    CJDIVI%J = A%J / B
  END FUNCTION CJDIVI
  PURE FUNCTION ZJDIVI(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZJDIVI
    ZJDIVI%J = A%J / B
  END FUNCTION ZJDIVI

  PURE FUNCTION CIDIVJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CIDIVJ
    CIDIVJ%J = -(A / B%J)
  END FUNCTION CIDIVJ
  PURE FUNCTION ZIDIVJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZIDIVJ
    ZIDIVJ%J = -(A / B%J)
  END FUNCTION ZIDIVJ

  PURE FUNCTION CJDIVR(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP), INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CJDIVR
    CJDIVR%J = A%J / B
  END FUNCTION CJDIVR
  PURE FUNCTION ZJDIVR(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP), INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZJDIVR
    ZJDIVR%J = A%J / B
  END FUNCTION ZJDIVR

  PURE FUNCTION CRDIVJ(A,B)
    IMPLICIT NONE
    REAL(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    TYPE(CIMAGINARY) :: CRDIVJ
    CRDIVJ%J = -(A / B%J)
  END FUNCTION CRDIVJ
  PURE FUNCTION ZRDIVJ(A,B)
    IMPLICIT NONE
    REAL(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    TYPE(ZIMAGINARY) :: ZRDIVJ
    ZRDIVJ%J = -(A / B%J)
  END FUNCTION ZRDIVJ

  PURE FUNCTION CJDIVC(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=SWP), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CJDIVC
    CJDIVC = CMPLX(AIMAG(B), REAL(B), SWP)
    CJDIVC = (A%J / ABS(CJDIVC)) * CJDIVC
  END FUNCTION CJDIVC
  PURE FUNCTION ZJDIVC(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=DWP), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZJDIVC
    ZJDIVC = CMPLX(AIMAG(B), REAL(B), DWP)
    ZJDIVC = (A%J / ABS(ZJDIVC)) * ZJDIVC
  END FUNCTION ZJDIVC

  PURE FUNCTION CCDIVJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=SWP) :: CCDIVJ
    CCDIVJ = CMPLX(AIMAG(A), -REAL(A), SWP) / B%J
  END FUNCTION CCDIVJ
  PURE FUNCTION ZCDIVJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    COMPLEX(KIND=DWP) :: ZCDIVJ
    ZCDIVJ = CMPLX(AIMAG(A), -REAL(A), DWP) / B%J
  END FUNCTION ZCDIVJ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  PURE FUNCTION CJEQJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: CJEQJ
    CJEQJ = (A%J .EQ. B%J)
  END FUNCTION CJEQJ
  PURE FUNCTION ZJEQJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: ZJEQJ
    ZJEQJ = (A%J .EQ. B%J)
  END FUNCTION ZJEQJ

  PURE FUNCTION CJEQI(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    LOGICAL :: CJEQI
    CJEQI = (B .EQ. 0) .AND. (A%J .EQ. S_ZERO)
  END FUNCTION CJEQI
  PURE FUNCTION ZJEQI(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    LOGICAL :: ZJEQI
    ZJEQI = (B .EQ. 0) .AND. (A%J .EQ. D_ZERO)
  END FUNCTION ZJEQI

  PURE FUNCTION CIEQJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    LOGICAL :: CIEQJ
    CIEQJ = (A .EQ. 0) .AND. (B%J .EQ. S_ZERO)
  END FUNCTION CIEQJ
  PURE FUNCTION ZIEQJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    LOGICAL :: ZIEQJ
    ZIEQJ = (A .EQ. 0) .AND. (B%J .EQ. D_ZERO)
  END FUNCTION ZIEQJ

  PURE FUNCTION CJEQR(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP), INTENT(IN) :: B
    LOGICAL :: CJEQR
    CJEQR = (B .EQ. S_ZERO) .AND. (A%J .EQ. B)
  END FUNCTION CJEQR
  PURE FUNCTION ZJEQR(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP), INTENT(IN) :: B
    LOGICAL :: ZJEQR
    ZJEQR = (B .EQ. D_ZERO) .AND. (A%J .EQ. B)
  END FUNCTION ZJEQR

  PURE FUNCTION CREQJ(A,B)
    IMPLICIT NONE
    REAL(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    LOGICAL :: CREQJ
    CREQJ = (A .EQ. S_ZERO) .AND. (B%J .EQ. A)
  END FUNCTION CREQJ
  PURE FUNCTION ZREQJ(A,B)
    IMPLICIT NONE
    REAL(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    LOGICAL :: ZREQJ
    ZREQJ = (A .EQ. D_ZERO) .AND. (B%J .EQ. A)
  END FUNCTION ZREQJ

  PURE FUNCTION CJEQC(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=SWP), INTENT(IN) :: B
    LOGICAL :: CJEQC
    CJEQC = (REAL(B) .EQ. S_ZERO) .AND. (A%J .EQ. AIMAG(B))
  END FUNCTION CJEQC
  PURE FUNCTION ZJEQC(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=DWP), INTENT(IN) :: B
    LOGICAL :: ZJEQC
    ZJEQC = (REAL(B) .EQ. D_ZERO) .AND. (A%J .EQ. AIMAG(B))
  END FUNCTION ZJEQC

  PURE FUNCTION CCEQJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    LOGICAL :: CCEQJ
    CCEQJ = (REAL(A) .EQ. S_ZERO) .AND. (B%J .EQ. AIMAG(A))
  END FUNCTION CCEQJ
  PURE FUNCTION ZCEQJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    LOGICAL :: ZCEQJ
    ZCEQJ = (REAL(A) .EQ. D_ZERO) .AND. (B%J .EQ. AIMAG(A))
  END FUNCTION ZCEQJ

  PURE FUNCTION CJNEJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: CJNEJ
    CJNEJ = (A%J .NE. B%J)
  END FUNCTION CJNEJ
  PURE FUNCTION ZJNEJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: ZJNEJ
    ZJNEJ = (A%J .NE. B%J)
  END FUNCTION ZJNEJ

  PURE FUNCTION CJNEI(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    LOGICAL :: CJNEI
    CJNEI = (B .NE. 0) .OR. (A%J .NE. S_ZERO)
  END FUNCTION CJNEI
  PURE FUNCTION ZJNEI(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    INTEGER, INTENT(IN) :: B
    LOGICAL :: ZJNEI
    ZJNEI = (B .NE. 0) .OR. (A%J .NE. D_ZERO)
  END FUNCTION ZJNEI

  PURE FUNCTION CINEJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    LOGICAL :: CINEJ
    CINEJ = (A .NE. 0) .OR. (B%J .NE. S_ZERO)
  END FUNCTION CINEJ
  PURE FUNCTION ZINEJ(A,B)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    LOGICAL :: ZINEJ
    ZINEJ = (A .NE. 0) .OR. (B%J .NE. D_ZERO)
  END FUNCTION ZINEJ

  PURE FUNCTION CJNER(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP), INTENT(IN) :: B
    LOGICAL :: CJNER
    CJNER = (B .NE. S_ZERO) .OR. (A%J .NE. B)
  END FUNCTION CJNER
  PURE FUNCTION ZJNER(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP), INTENT(IN) :: B
    LOGICAL :: ZJNER
    ZJNER = (B .NE. D_ZERO) .OR. (A%J .NE. B)
  END FUNCTION ZJNER

  PURE FUNCTION CRNEJ(A,B)
    IMPLICIT NONE
    REAL(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    LOGICAL :: CRNEJ
    CRNEJ = (A .NE. S_ZERO) .OR. (B%J .NE. A)
  END FUNCTION CRNEJ
  PURE FUNCTION ZRNEJ(A,B)
    IMPLICIT NONE
    REAL(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    LOGICAL :: ZRNEJ
    ZRNEJ = (A .NE. D_ZERO) .OR. (B%J .NE. A)
  END FUNCTION ZRNEJ

  PURE FUNCTION CJNEC(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=SWP), INTENT(IN) :: B
    LOGICAL :: CJNEC
    CJNEC = (REAL(B) .NE. S_ZERO) .OR. (A%J .NE. AIMAG(B))
  END FUNCTION CJNEC
  PURE FUNCTION ZJNEC(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    COMPLEX(KIND=DWP), INTENT(IN) :: B
    LOGICAL :: ZJNEC
    ZJNEC = (REAL(B) .NE. D_ZERO) .OR. (A%J .NE. AIMAG(B))
  END FUNCTION ZJNEC

  PURE FUNCTION CCNEJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=SWP), INTENT(IN) :: A
    TYPE(CIMAGINARY), INTENT(IN) :: B
    LOGICAL :: CCNEJ
    CCNEJ = (REAL(A) .NE. S_ZERO) .OR. (B%J .NE. AIMAG(A))
  END FUNCTION CCNEJ
  PURE FUNCTION ZCNEJ(A,B)
    IMPLICIT NONE
    COMPLEX(KIND=DWP), INTENT(IN) :: A
    TYPE(ZIMAGINARY), INTENT(IN) :: B
    LOGICAL :: ZCNEJ
    ZCNEJ = (REAL(A) .NE. D_ZERO) .OR. (B%J .NE. AIMAG(A))
  END FUNCTION ZCNEJ

  PURE FUNCTION CJLTJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: CJLTJ
    CJLTJ = (A%J .LT. B%J)
  END FUNCTION CJLTJ
  PURE FUNCTION ZJLTJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: ZJLTJ
    ZJLTJ = (A%J .LT. B%J)
  END FUNCTION ZJLTJ

  PURE FUNCTION CJLEJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: CJLEJ
    CJLEJ = (A%J .LE. B%J)
  END FUNCTION CJLEJ
  PURE FUNCTION ZJLEJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: ZJLEJ
    ZJLEJ = (A%J .LE. B%J)
  END FUNCTION ZJLEJ

  PURE FUNCTION CJGEJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: CJGEJ
    CJGEJ = (A%J .GE. B%J)
  END FUNCTION CJGEJ
  PURE FUNCTION ZJGEJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: ZJGEJ
    ZJGEJ = (A%J .GE. B%J)
  END FUNCTION ZJGEJ

  PURE FUNCTION CJGTJ(A,B)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: CJGTJ
    CJGTJ = (A%J .GT. B%J)
  END FUNCTION CJGTJ
  PURE FUNCTION ZJGTJ(A,B)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A,B
    LOGICAL :: ZJGTJ
    ZJGTJ = (A%J .GT. B%J)
  END FUNCTION ZJGTJ

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  ELEMENTAL FUNCTION CJREAL(A)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP) :: CJREAL
    CJREAL = S_ZERO
  END FUNCTION CJREAL
  ELEMENTAL FUNCTION ZJREAL(A)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP) :: ZJREAL
    ZJREAL = D_ZERO
  END FUNCTION ZJREAL

  ELEMENTAL FUNCTION CJIMAG(A)
    IMPLICIT NONE
    TYPE(CIMAGINARY), INTENT(IN) :: A
    REAL(KIND=SWP) :: CJIMAG
    CJIMAG = A%J
  END FUNCTION CJIMAG
  ELEMENTAL FUNCTION ZJIMAG(A)
    IMPLICIT NONE
    TYPE(ZIMAGINARY), INTENT(IN) :: A
    REAL(KIND=DWP) :: ZJIMAG
    ZJIMAG = A%J
  END FUNCTION ZJIMAG

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

END MODULE VN_IMAGINARY_F
