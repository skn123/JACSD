PROGRAM DTGSJA_TEST
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, ERROR_UNIT
  USE BINIO
  IMPLICIT NONE

  ! CHARACTER, PARAMETER :: JOBU='U',JOBV='V',JOBQ='Q'
  CHARACTER, PARAMETER :: JOBU='N',JOBV='N',JOBQ='N'
  INTEGER, PARAMETER :: ALIGNB = 64, FNL = 252
  DOUBLE PRECISION, PARAMETER :: D_ZERO = 0.0D0

  CHARACTER(LEN=FNL,KIND=c_char) :: FN
  INTEGER :: M,N,P,K,L, LDA,LDB,LDU,LDV,LDQ, INFO, SZ,FD
  DOUBLE PRECISION :: ANORM,BNORM, ULP,UNFL, TOLA,TOLB

  DOUBLE PRECISION, ALLOCATABLE :: A(:,:),B(:,:),U(:,:),V(:,:),Q(:,:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: A,B,U,V,Q
  DOUBLE PRECISION, ALLOCATABLE :: ALPHA(:),BETA(:),WORK(:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: ALPHA,BETA, WORK

  INTEGER(KIND=c_long_long), EXTERNAL :: PVN_TIME_MONO_TICKS, PVN_TIME_MONO_FREQ
  DOUBLE PRECISION, EXTERNAL :: DLANGE, DLAMCH
  EXTERNAL :: DTGSJA, DLASRT

  CALL READCL(M, N, FN, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     ERROR STOP 'READCL'
  END IF
  P = M

  CALL BOPEN_RO((TRIM(FN)//c_char_'.Y'), SZ, FD)
  IF (FD .LT. 0) ERROR STOP 'BOPEN_Y_RO'
  LDA = M; ALLOCATE(A(LDA,N)); A = D_ZERO

  ! read F into A
  CALL BREAD2(FD, A, M, N, SZ, INFO)
  IF (INFO .NE. 0) ERROR STOP 'BREAD_Y'
  CALL BCLOSE(FD)

  CALL BOPEN_RO((TRIM(FN)//c_char_'.W'), SZ, FD)
  IF (FD .LT. 0) ERROR STOP 'BOPEN_W_RO'
  LDB = P; ALLOCATE(B(LDB,N)); B = D_ZERO

  ! read G into B
  CALL BREAD2(FD, B, M, N, SZ, INFO)
  IF (INFO .NE. 0) ERROR STOP 'BREAD_W'
  CALL BCLOSE(FD)

  LDU = M !; ALLOCATE(U(LDU,M)); U = D_ZERO
  LDV = P !; ALLOCATE(V(LDV,P)); V = D_ZERO
  LDQ = N !; ALLOCATE(Q(LDQ,N)); Q = D_ZERO

  ALLOCATE(ALPHA(N)); ALPHA = D_ZERO
  ALLOCATE(BETA(N)); BETA = D_ZERO
  ALLOCATE(WORK(2 * N)); WORK = D_ZERO

  K = 0; L = N; FD = 0
  SZ = INT(PVN_TIME_MONO_TICKS())

  ANORM = DLANGE('1', M, N, A, LDA, WORK)
  BNORM = DLANGE('1', P, N, B, LDB, WORK)

  ULP = DLAMCH('P') ! Precision
  UNFL = DLAMCH('S') ! Safe Minimum

  TOLA = MAX(M, N) * MAX(ANORM, UNFL) * ULP
  TOLB = MAX(P, N) * MAX(BNORM, UNFL) * ULP

  CALL DTGSJA(JOBU,JOBV,JOBQ, M,P,N,K,L, A,LDA, B,LDB, TOLA,TOLB, ALPHA,BETA, U,LDU, V,LDV, Q,LDQ, WORK, FD,INFO)
  SZ = INT(PVN_TIME_MONO_TICKS()) - SZ
  WRITE (OUTPUT_UNIT,'(A,F15.6,A,I3,A)') 'DTGSJA took ', (SZ / DBLE(PVN_TIME_MONO_FREQ())), ' s for ', FD, ' cycles.'
  FLUSH(OUTPUT_UNIT)

  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I3,A)',ADVANCE='NO') INFO, ' '
     ERROR STOP 'DTGSJA'
  END IF
  IF ((K + L) .NE. N) THEN
     WRITE (ERROR_UNIT,'(4(I6,A))',ADVANCE='NO') K,'+', L,'=', (K+L),'<', N,' '
     ERROR STOP 'DTGSJA numerical rank'
  END IF

  !DIR$ VECTOR ALWAYS ALIGNED
  DO INFO = 1, N
     WORK(INFO) = ALPHA(INFO) / BETA(INFO)
  END DO
  CALL DLASRT('D', N, WORK, INFO)
  IF (INFO .NE. 0) ERROR STOP 'DLASRT'

  SZ = INT(N * C_SIZEOF(D_ZERO)); FD = -1
  CALL BOPEN_RW((TRIM(FN)//c_char_'.SS'), SZ, FD)
  IF (FD .LT. 0) ERROR STOP 'BOPEN_SS_RW'
  CALL BWRITE1(FD, WORK, N, INFO)
  IF (INFO .NE. SZ) ERROR STOP 'BWRITE_SS'
  CALL BCLOSE(FD)

  IF (ALLOCATED(WORK)) DEALLOCATE(WORK)
  IF (ALLOCATED(BETA)) DEALLOCATE(BETA)
  IF (ALLOCATED(ALPHA)) DEALLOCATE(ALPHA)

  IF (ALLOCATED(Q)) DEALLOCATE(Q)
  IF (ALLOCATED(V)) DEALLOCATE(V)
  IF (ALLOCATED(U)) DEALLOCATE(U)

  IF (ALLOCATED(B)) DEALLOCATE(B)
  IF (ALLOCATED(A)) DEALLOCATE(A)

CONTAINS

  SUBROUTINE READCL(M, N, FN, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(OUT) :: M, N, INFO
    CHARACTER(LEN=*,KIND=c_char), INTENT(OUT) :: FN

    CHARACTER(LEN=FNL) :: ARG
    INTEGER :: TMP

    INFO = 0
    IF (COMMAND_ARGUMENT_COUNT() .NE. 3) ERROR STOP 'dtgsja_test.exe M N FN'
    ARG = '                        '
    TMP = 0

    CALL GET_COMMAND_ARGUMENT(1, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -1
       RETURN
    END IF
    READ (ARG,*) M
    IF (M .LE. 0) THEN
       INFO = 1
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(2, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF
    READ (ARG,*) N
    IF ((N .LE. 0) .OR. (M .LT. N)) THEN
       INFO = 2
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(3, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -3
       RETURN
    END IF
    FN = TRIM(ARG)
    IF (LEN_TRIM(FN) .LE. 0) THEN
       INFO = 3
       RETURN
    END IF
  END SUBROUTINE READCL

  SUBROUTINE BREAD2(FD, YW, M, N, SZ, INFO)
    USE, INTRINSIC :: ISO_C_BINDING
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: FD, M, N
    DOUBLE PRECISION, INTENT(OUT) :: YW(M,N)
    INTEGER, INTENT(OUT) :: SZ, INFO

    SZ = INT(M * (N * C_SIZEOF(D_ZERO)))
    IF (INT(BREADD(INT(FD,c_int), YW, INT(SZ,c_size_t), 0_c_size_t)) .EQ. SZ) THEN
       INFO = 0
    ELSE ! error
       INFO = 1
    END IF
  END SUBROUTINE BREAD2

  SUBROUTINE BWRITE1(FD, SS, N, INFO)
    USE, INTRINSIC :: ISO_C_BINDING
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: FD, N
    DOUBLE PRECISION, INTENT(IN) :: SS(N)
    INTEGER, INTENT(OUT) :: INFO

    INFO = INT(BWRITED(INT(FD,c_int), SS, C_SIZEOF(SS), 0_c_size_t))
  END SUBROUTINE BWRITE1

END PROGRAM DTGSJA_TEST
