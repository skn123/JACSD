! Change OMP to $OMP in the comments below to enable OpenMP loop parallelization.

SUBROUTINE DGENDAT(N, ISEED, QS_F, QS_G, QL_X, QF, DF, QG, DG, QX, DX, QWORK, INFO)
  IMPLICIT NONE
#include "qx_wp.fi"

  REAL(KIND=WP), PARAMETER :: ZERO = 0.0E0_WP, ONE = 1.0E0_WP

  INTEGER, INTENT(IN) :: N, ISEED(4)
  REAL(KIND=WP), INTENT(IN) :: QS_F(N), QS_G(N), QL_X(N)
  DOUBLE PRECISION, INTENT(OUT) :: DF(N,N), DG(N,N), DX(N,N)
  REAL(KIND=WP), INTENT(OUT) :: QF(N,N), QG(N,N), QX(N,N), QWORK(3*N)
  INTEGER, INTENT(OUT) :: INFO

  INTEGER :: P, Q
  EXTERNAL :: SEEDOK, QGEMM, QLAGSY, QLAROR

  INFO = 0

  IF (N .LT. 0) THEN
     INFO = -1
     RETURN
  END IF
  CALL SEEDOK(ISEED, INFO)
  IF (INFO .NE. 0) THEN
     INFO = -2
     RETURN
  END IF

  CALL QLAGSY(N, N-1, QL_X, QX, N, ISEED, QWORK, INFO)
  IF (INFO .NE. 0) RETURN
  !OMP PARALLEL DO DEFAULT(NONE) SHARED(N,QX,DX) PRIVATE(P,Q)
  DO Q = 1, N
     DO P = 1, N
        DX(P,Q) = DBLE(QX(P,Q))
     END DO
  END DO
  !OMP END PARALLEL DO

  !OMP PARALLEL DO DEFAULT(NONE) SHARED(N,QF,QS_F) PRIVATE(P,Q)
  DO Q = 1, N
     DO P = 1, Q-1
        QF(P,Q) = ZERO
     END DO
     QF(Q,Q) = QS_F(Q)
     DO P = Q+1, N
        QF(P,Q) = ZERO
     END DO
  END DO
  !OMP END PARALLEL DO

  CALL QLAROR('L', 'N', N, N, QF, N, ISEED, QWORK, INFO)
  IF (INFO .NE. 0) RETURN

  CALL QGEMM('N', 'N', N, N, N, ONE, QF, N, QX, N, ZERO, QG, N)
  !OMP PARALLEL DO DEFAULT(NONE) SHARED(N,QG,DF) PRIVATE(P,Q)
  DO Q = 1, N
     DO P = 1, N
        DF(P,Q) = DBLE(QG(P,Q))
     END DO
  END DO
  !OMP END PARALLEL DO

  !OMP PARALLEL DO DEFAULT(NONE) SHARED(N,QG,QS_G) PRIVATE(P,Q)
  DO Q = 1, N
     DO P = 1, Q-1
        QG(P,Q) = ZERO
     END DO
     QG(Q,Q) = QS_G(Q)
     DO P = Q+1, N
        QG(P,Q) = ZERO
     END DO
  END DO
  !OMP END PARALLEL DO

  CALL QLAROR('L', 'N', N, N, QG, N, ISEED, QWORK, INFO)
  IF (INFO .NE. 0) RETURN

  CALL QGEMM('N', 'N', N, N, N, ONE, QG, N, QX, N, ZERO, QF, N)
  !OMP PARALLEL DO DEFAULT(NONE) SHARED(N,QF,DG) PRIVATE(P,Q)
  DO Q = 1, N
     DO P = 1, N
        DG(P,Q) = DBLE(QF(P,Q))
     END DO
  END DO
  !OMP END PARALLEL DO
END SUBROUTINE DGENDAT
