! A simple sanity checker for an MPI implementation.
PROGRAM MPI_TEST
#ifdef USE_MPI_F08
  USE mpi_f08
#else
  USE mpi
#endif
  USE, INTRINSIC :: ISO_C_BINDING, ONLY: C_SIZEOF
  IMPLICIT NONE
  CHARACTER(LEN=MPI_MAX_LIBRARY_VERSION_STRING) :: VERSION
  CHARACTER(LEN=MPI_MAX_PROCESSOR_NAME) :: NAME
  INTEGER :: VER, SUBVER, REQUIRED, PROVIDED, IERROR
  LOGICAL :: FLAG
  WRITE (*,'(A,I1)') 'C_SIZEOF(INTEGER): ', INT(C_SIZEOF(0))
  CALL MPI_GET_VERSION(VER, SUBVER, IERROR)
  IF (IERROR .NE. MPI_SUCCESS) STOP 'MPI_GET_VERSION'
  WRITE (*,'(2(A,I1))') 'MPI_GET_VERSION: ', VER, '.', SUBVER
  CALL MPI_GET_LIBRARY_VERSION(VERSION, PROVIDED, IERROR)
  IF (IERROR .NE. MPI_SUCCESS) STOP 'MPI_GET_LIBRARY_VERSION'
  WRITE (*,'(2A)') 'MPI_GET_LIBRARY_VERSION: ', VERSION(1:PROVIDED)
  CALL MPI_INITIALIZED(FLAG, IERROR)
  IF (IERROR .NE. MPI_SUCCESS) STOP 'MPI_INITIALIZED'
  IF (.NOT. FLAG) THEN
     CALL MPI_FINALIZED(FLAG, IERROR)
     IF (IERROR .NE. MPI_SUCCESS) STOP 'MPI_FINALIZED'
     IF (FLAG) STOP 'MPI FINALIZED'
     REQUIRED = MPI_THREAD_MULTIPLE
     CALL MPI_INIT_THREAD(REQUIRED, PROVIDED, IERROR)
     IF (IERROR .NE. MPI_SUCCESS) STOP 'MPI_INIT_THREAD'
     SELECT CASE (PROVIDED)
     CASE (MPI_THREAD_SINGLE)
        WRITE (*,'(A)') 'MPI_INIT_THREAD: MPI_THREAD_SINGLE'
     CASE (MPI_THREAD_FUNNELED)
        WRITE (*,'(A)') 'MPI_INIT_THREAD: MPI_THREAD_FUNNELED'
     CASE (MPI_THREAD_SERIALIZED)
        WRITE (*,'(A)') 'MPI_INIT_THREAD: MPI_THREAD_SERIALIZED'
     CASE (MPI_THREAD_MULTIPLE)
        WRITE (*,'(A)') 'MPI_INIT_THREAD: MPI_THREAD_MULTIPLE'
     CASE DEFAULT
        STOP 'MPI INIT THREAD'
     END SELECT
     CALL MPI_GET_PROCESSOR_NAME(NAME, REQUIRED, IERROR)
     IF (IERROR .NE. MPI_SUCCESS) STOP 'MPI_GET_PROCESSOR_NAME'
     WRITE (*,'(2A)') 'MPI_GET_PROCESSOR_NAME: ', NAME(1:REQUIRED)
  END IF
  CALL MPI_FINALIZE(IERROR)
  IF (IERROR .NE. MPI_SUCCESS) STOP 'MPI_FINALIZE'
END PROGRAM MPI_TEST
