PROGRAM ZGENHSVD
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, ERROR_UNIT
  USE BIO
  USE SEED
  USE LAMGEN
  USE DATGEN
  IMPLICIT NONE
#include "qx_wp.fi"

  INTEGER, PARAMETER :: FNL = 252
  DOUBLE PRECISION, PARAMETER :: ZERO = 0.0D0

  ! command-line parameters
  INTEGER :: SEEDI, N, IDIST, INFO
  DOUBLE PRECISION :: EPS, SCAL
  CHARACTER(LEN=FNL) :: LAMBDA, FIL

  INTEGER :: ISEED(4), P

  INTEGER, ALLOCATABLE :: J(:), IWORK(:)
  DOUBLE PRECISION, ALLOCATABLE :: DLAMBDA(:)
  REAL(KIND=WP), ALLOCATABLE :: QLAMBDA(:)
  COMPLEX(KIND=WP), ALLOCATABLE :: XWORK(:)

  DOUBLE COMPLEX, ALLOCATABLE :: ZA(:,:)
  COMPLEX(KIND=WP), ALLOCATABLE :: XA(:,:)

  CALL READCL(LAMBDA, SEEDI, N, FIL, IDIST, EPS, SCAL, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'READCL'
  END IF

  CALL SEEDIX(SEEDI, ISEED, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'SEEDIX'
  END IF

  ALLOCATE(DLAMBDA(N))
  IF (IDIST .NE. 0) THEN
     CALL DGENLAM(N, ISEED, IDIST, EPS, SCAL, DLAMBDA, P, INFO)
  ELSE
     CALL DTXTLAM(LAMBDA, N, DLAMBDA, P, INFO)
  END IF
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'LAMBDA'
  END IF

  ALLOCATE(ZA(N,N))
  ALLOCATE(J(N))

  ALLOCATE(XA(N,N))
  ALLOCATE(QLAMBDA(N))
  DO P = 1, N
     QLAMBDA(P) = REAL(DLAMBDA(P),WP)
  END DO
  P = 3 * N
  ALLOCATE(IWORK(P))
  P = 2 * N
  ALLOCATE(XWORK(P))

  CALL ZGENDAT(N, ISEED, QLAMBDA, XA, ZA, J, IWORK, XWORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'ZGENDAT'
  END IF

  DEALLOCATE(XWORK)
  DEALLOCATE(IWORK)
  DEALLOCATE(QLAMBDA)
  DEALLOCATE(XA)

  P = -1
  CALL BIO_OPEN(P, TRIM(FIL)//'.J', 'WO', INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_OPEN(J)'
  END IF
  CALL BIO_WRITE_I1(P, N, J, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_WRITE_I1(J)'
  END IF
  CALL BIO_CLOSE(P, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_CLOSE(J)'
  END IF
  DEALLOCATE(J)

  P = -1
  CALL BIO_OPEN(P, TRIM(FIL)//'.Y', 'WO', INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_OPEN(Y)'
  END IF
  CALL BIO_WRITE_Z2(P, N, N, ZA, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_WRITE_Z2(Y)'
  END IF
  CALL BIO_CLOSE(P, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_CLOSE(Y)'
  END IF
  DEALLOCATE(ZA)

  P = -1
  CALL BIO_OPEN(P, TRIM(FIL)//'.LY', 'WO', INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_OPEN(LY)'
  END IF
  CALL BIO_WRITE_D1(P, N, DLAMBDA, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_WRITE_D1(LY)'
  END IF
  CALL BIO_CLOSE(P, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,*) INFO
     ERROR STOP 'BIO_CLOSE(LY)'
  END IF
  DEALLOCATE(DLAMBDA)

CONTAINS

  SUBROUTINE READCL(LAMBDA, SEEDI, N, FIL, IDIST, EPS, SCAL, INFO)
    IMPLICIT NONE

    INTEGER, INTENT(OUT) :: SEEDI, N, IDIST, INFO
    DOUBLE PRECISION, INTENT(OUT) :: EPS, SCAL
    CHARACTER(LEN=*), INTENT(OUT) :: LAMBDA, FIL

    INTEGER, PARAMETER :: NRQP = 4
    INTEGER :: NXTA
    CHARACTER(LEN=FNL) :: CAS

    SEEDI = 0
    N = 0
    IDIST = 0
    INFO = 0

    EPS = ZERO
    SCAL = ZERO

    LAMBDA = ''
    FIL = ''

    NXTA = NRQP
    CAS = ''

    IF (COMMAND_ARGUMENT_COUNT() .LT. NRQP) THEN
       WRITE (OUTPUT_UNIT,*) 'zgenhsvd.exe LAMBDA SEEDIX N FILE [ LAMBDA_PARAMS ]'
       WRITE (OUTPUT_UNIT,*) '>> COMMAND LINE (INPUT) ARGUMENTS <<'
       WRITE (OUTPUT_UNIT,*) 'LAMBDA  : \Lambda(A); 1, 2, 3, or FILENAME'
       WRITE (OUTPUT_UNIT,*) 'IDIST123: 1 [uniform (0,1)], 2 [uniform(-1,1)], or 3 [normal(0,1)]'
       WRITE (OUTPUT_UNIT,*) 'FILENAME: LAMBDA.txt: max 256 chars, >= N lines [each line = one real value]'
       WRITE (OUTPUT_UNIT,*) 'SEEDIX  : index of hard-coded pRNG seed (see seedix.F90); 1 or 2'
       WRITE (OUTPUT_UNIT,*) 'N       : order of the output matrix: > 0'
       WRITE (OUTPUT_UNIT,*) 'FILE    : output file name prefix: max 256 chars'
       WRITE (OUTPUT_UNIT,*) 'LAMBDA  ; LAMBDA_PARAMS if LAMBDA is IDIST123'
       WRITE (OUTPUT_UNIT,*) ' EPS    : \lambda''_i survives iff |\lambda''_i| > EPS'
       WRITE (OUTPUT_UNIT,*) ' SCALE  : final \lambda_i = \lambda''_i * SCALE'
       WRITE (OUTPUT_UNIT,*) '<< OUTPUT DATASETS >>'
       WRITE (OUTPUT_UNIT,*) 'FILE.Y  : double complex(N,N); a factor F (A = F^H J F)'
       WRITE (OUTPUT_UNIT,*) 'FILE.J  : integer*8(N); diagonal of the sign matrix J'
       WRITE (OUTPUT_UNIT,*) 'FILE.LY : double precision(N); \Lambda(A) as read/generated'
       INFO = 1
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(1, LAMBDA, STATUS=INFO)
    IF (INFO .NE. 0) THEN
       INFO = -1
       RETURN
    END IF
    IF (TRIM(LAMBDA) .EQ. '1') THEN
       IDIST = 1
    ELSE IF (TRIM(LAMBDA) .EQ. '2') THEN
       IDIST = 2
    ELSE IF (TRIM(LAMBDA) .EQ. '3') THEN
       IDIST = 3
    END IF

    CALL GET_COMMAND_ARGUMENT(2, CAS, STATUS=INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF
    READ (CAS,*) SEEDI
    IF (SEEDI .LE. 0) THEN
       INFO = -2
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(3, CAS, STATUS=INFO)
    IF (INFO .NE. 0) THEN
       INFO = -3
       RETURN
    END IF
    READ (CAS,*) N
    IF (N .LE. 0) THEN
       INFO = -3
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(4, FIL, STATUS=INFO)
    IF (INFO .NE. 0) THEN
       INFO = -4
       RETURN
    END IF

    IF (IDIST .NE. 0) THEN
       NXTA = NXTA + 1
       CALL GET_COMMAND_ARGUMENT(NXTA, CAS, STATUS=INFO)
       IF (INFO .NE. 0) THEN
          INFO = -NXTA
          RETURN
       END IF
       READ (CAS,*) EPS
       IF (EPS .LT. ZERO) THEN
          INFO = -NXTA
          RETURN
       END IF
       NXTA = NXTA + 1
       CALL GET_COMMAND_ARGUMENT(NXTA, CAS, STATUS=INFO)
       IF (INFO .NE. 0) THEN
          INFO = -NXTA
          RETURN
       END IF
       READ (CAS,*) SCAL
       IF (SCAL .EQ. ZERO) THEN
          INFO = -NXTA
          RETURN
       END IF
    END IF
  END SUBROUTINE READCL

END PROGRAM ZGENHSVD
