MODULE DATGEN
  USE SEED
  USE INDEFF
  IMPLICIT NONE
#include "qx_wp.fi"

  PRIVATE :: WP

CONTAINS

  SUBROUTINE DGENDAT(N, ISEED, QLAMBDA, QA, DA, J, IWORK, QWORK, INFO)
    IMPLICIT NONE

    INTEGER, INTENT(IN) :: N, ISEED(4)
    REAL(KIND=WP), INTENT(IN) :: QLAMBDA(N)
    DOUBLE PRECISION, INTENT(OUT) :: DA(N,N)
    REAL(KIND=WP), INTENT(OUT) :: QA(N,N), QWORK(2*N)
    INTEGER, INTENT(OUT) :: J(N), IWORK(3*N), INFO

    INTEGER :: P, Q, NRANK, NPLUS, N2PIV
    EXTERNAL :: QLAGSY

    INFO = 0

    IF (N .LT. 0) THEN
       INFO = -1
       RETURN
    END IF
    CALL SEEDOK(ISEED, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF

    CALL QLAGSY(N, N-1, QLAMBDA, QA, N, ISEED, QWORK, INFO)
    IF (INFO .NE. 0) RETURN
    CALL QSIF(N, QA, N, NRANK, NPLUS, N2PIV, IWORK(1), J, IWORK(N+1), IWORK(2*N+1), INFO)
    WRITE (*,*) NRANK, NPLUS, N2PIV
    IF (INFO .NE. 0) RETURN
    !$OMP PARALLEL DO DEFAULT(NONE) SHARED(N,QA,DA) PRIVATE(P,Q)
    DO Q = 1, N
       DO P = 1, N
          DA(P,Q) = DBLE(QA(P,Q))
       END DO
    END DO
    !$OMP END PARALLEL DO
  END SUBROUTINE DGENDAT

  SUBROUTINE ZGENDAT(N, ISEED, QLAMBDA, XA, ZA, J, IWORK, XWORK, INFO)
    IMPLICIT NONE

    INTEGER, INTENT(IN) :: N, ISEED(4)
    REAL(KIND=WP), INTENT(IN) :: QLAMBDA(N)
    DOUBLE COMPLEX, INTENT(OUT) :: ZA(N,N)
    COMPLEX(KIND=WP), INTENT(OUT) :: XA(N,N), XWORK(2*N)
    INTEGER, INTENT(OUT) :: J(N), IWORK(3*N), INFO

    INTEGER :: P, Q, NRANK, NPLUS, N2PIV
    EXTERNAL :: XLAGHE

    INFO = 0

    IF (N .LT. 0) THEN
       INFO = -1
       RETURN
    END IF
    CALL SEEDOK(ISEED, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF

    CALL XLAGHE(N, N-1, QLAMBDA, XA, N, ISEED, XWORK, INFO)
    IF (INFO .NE. 0) RETURN
    CALL XHIF(N, XA, N, NRANK, NPLUS, N2PIV, IWORK(1), J, IWORK(N+1), IWORK(2*N+1), INFO)
    WRITE (*,*) NRANK, NPLUS, N2PIV
    IF (INFO .NE. 0) RETURN
    !$OMP PARALLEL DO DEFAULT(NONE) SHARED(N,XA,ZA) PRIVATE(P,Q)
    DO Q = 1, N
       DO P = 1, N
          ZA(P,Q) = DCMPLX(DBLE(REAL(XA(P,Q))), DBLE(AIMAG(XA(P,Q))))
       END DO
    END DO
    !$OMP END PARALLEL DO
  END SUBROUTINE ZGENDAT

END MODULE DATGEN
