PROGRAM SVALSERR
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: OUTPUT_UNIT, ERROR_UNIT
  USE BIO
  IMPLICIT NONE
#include "qx_wp.fi"

  DOUBLE PRECISION, PARAMETER :: D_ZERO = 0.0D0
  REAL(KIND=WP), PARAMETER :: Q_ZERO = 0.0_WP, Q_ONE = 1.0_WP

  INTEGER, PARAMETER :: FNL = 252
  CHARACTER(LEN=2), PARAMETER :: ACT = 'RO'

  CHARACTER(LEN=FNL) :: FTRUE, FCOMP
  DOUBLE PRECISION :: MINAE, MAXAE, AVGAE, MINRE, MAXRE, AVGRE
  INTEGER :: N, INFO, U

  DOUBLE PRECISION, ALLOCATABLE :: T(:), C(:)
  INTEGER, ALLOCATABLE :: J(:)

  EXTERNAL :: DLASRT

  CALL READCL(N, FTRUE, FCOMP, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'READCL'
  END IF

  ALLOCATE(T(N))
  ALLOCATE(C(N))
  ALLOCATE(J(N))

  WRITE (OUTPUT_UNIT,'(A)') '"Lx","MINAE","MAXAE","AVGAE","MINRE","MAXRE","AVGRE"'
  FLUSH(OUTPUT_UNIT)

  U = -1
  CALL BIO_OPEN(U, TRIM(FTRUE)//'.LY', ACT, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_OPEN(LY)'
  END IF
  CALL BIO_READ_D1(U, N, T, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_READ_D1(LY)'
  END IF
  CALL BIO_CLOSE(U, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_CLOSE(LY)'
  END IF
  CALL DLASRT('D', N, T, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'DLASRT(LY)'
  END IF

  U = -1
  CALL BIO_OPEN(U, TRIM(FTRUE)//'.J', ACT, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_OPEN(J)'
  END IF
  CALL BIO_READ_I1(U, N, J, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_READ_I1(J)'
  END IF
  CALL BIO_CLOSE(U, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_CLOSE(J)'
  END IF

  U = -1
  CALL BIO_OPEN(U, TRIM(FCOMP)//'.SY', ACT, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_OPEN(SY)'
  END IF
  CALL BIO_READ_D1(U, N, C, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_READ_D1(SY)'
  END IF
  CALL BIO_CLOSE(U, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'BIO_CLOSE(SY)'
  END IF

  DO U = 1, N
     C(U) = C(U) * C(U) * J(U)
  END DO

  CALL DLASRT('D', N, C, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'DLASRT(SY)'
  END IF

  CALL ERRSTATS(N, T, C, MINAE, MAXAE, AVGAE, MINRE, MAXRE, AVGRE, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ERROR_UNIT,'(I2,A)',ADVANCE='NO') INFO, ' '
     FLUSH(ERROR_UNIT)
     ERROR STOP 'ERRSTATS(SY)'
  END IF
  WRITE (OUTPUT_UNIT,'(A,6(A,ES24.17E3))') '"LY"', ',',MINAE,',',MAXAE,',',AVGAE,',',MINRE,',',MAXRE,',',AVGRE
  FLUSH(OUTPUT_UNIT)

  DEALLOCATE(J)
  DEALLOCATE(C)
  DEALLOCATE(T)

CONTAINS

  PURE SUBROUTINE ERRSTATS(N, T, C, MINAE, MAXAE, AVGAE, MINRE, MAXRE, AVGRE, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: N
    DOUBLE PRECISION, INTENT(IN) :: T(N), C(N)
    DOUBLE PRECISION, INTENT(OUT) :: MINAE, MAXAE, AVGAE, MINRE, MAXRE, AVGRE
    INTEGER, INTENT(OUT) :: INFO

    REAL(KIND=WP) :: Q_MINAE, Q_MAXAE, Q_AVGAE, Q_MINRE, Q_MAXRE, Q_AVGRE
    REAL(KIND=WP) :: Q_AE, Q_RE, Q_ONE_N
    INTEGER :: I

    IF (N .LT. 0) THEN
       INFO = -1
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    MINAE = D_ZERO
    MAXAE = D_ZERO
    AVGAE = D_ZERO

    MINRE = D_ZERO
    MAXRE = D_ZERO
    AVGRE = D_ZERO

    IF (N .EQ. 0) RETURN

    Q_MINAE = Q_ZERO
    Q_MAXAE = Q_ZERO
    Q_AVGAE = Q_ZERO

    Q_MINRE = Q_ZERO
    Q_MAXRE = Q_ZERO
    Q_AVGRE = Q_ZERO

    Q_ONE_N = Q_ONE / N

    DO I = 1, N
       Q_AE = ABS(REAL(T(I), WP) - REAL(C(I), WP))
       IF (Q_AE .LT. Q_MINAE) Q_MINAE = Q_AE
       IF (Q_AE .GT. Q_MAXAE) Q_MAXAE = Q_AE
       Q_AVGAE = Q_AVGAE + Q_AE * Q_ONE_N

       Q_RE = Q_AE / ABS(REAL(T(I), WP))
       IF (.NOT. (Q_RE .EQ. Q_RE)) Q_RE = Q_ZERO
       IF (Q_RE .LT. Q_MINRE) Q_MINRE = Q_RE
       IF (Q_RE .GT. Q_MAXRE) Q_MAXRE = Q_RE
       Q_AVGRE = Q_AVGRE + Q_RE * Q_ONE_N
    END DO

    MINAE = DBLE(Q_MINAE)
    MAXAE = DBLE(Q_MAXAE)
    AVGAE = DBLE(Q_AVGAE)

    MINRE = DBLE(Q_MINRE)
    MAXRE = DBLE(Q_MAXRE)
    AVGRE = DBLE(Q_AVGRE)
  END SUBROUTINE ERRSTATS

  SUBROUTINE READCL(N, FTRUE, FCOMP, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(OUT) :: N, INFO
    CHARACTER(LEN=*), INTENT(OUT) :: FTRUE, FCOMP

    CHARACTER(LEN=FNL) :: ARG
    INTEGER :: TMP

    INFO = 0
    IF (COMMAND_ARGUMENT_COUNT() .NE. 3) ERROR STOP 'svalserr.exe N FTRUE FCOMP'

    CALL GET_COMMAND_ARGUMENT(1, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -1
       RETURN
    END IF
    READ (ARG,*) N
    IF (N .LE. 0) THEN
       INFO = 1
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(2, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF
    FTRUE = TRIM(ARG)
    IF (LEN_TRIM(FTRUE) .LE. 0) THEN
       INFO = 2
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(3, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -3
       RETURN
    END IF
    FCOMP = TRIM(ARG)
    IF (LEN_TRIM(FCOMP) .LE. 0) THEN
       INFO = 3
       RETURN
    END IF
  END SUBROUTINE READCL

END PROGRAM SVALSERR
