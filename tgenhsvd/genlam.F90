SUBROUTINE DGENLAM(N, ISEED, IDIST, EPS, SCAL, LAM, NPOS, INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: N, IDIST
  INTEGER, INTENT(INOUT) :: ISEED(4)
  DOUBLE PRECISION, INTENT(IN) :: EPS, SCAL
  DOUBLE PRECISION, INTENT(OUT) :: LAM(N)
  INTEGER, INTENT(OUT) :: NPOS, INFO

  DOUBLE PRECISION, PARAMETER :: ZERO = 0.0D0, ONE = 1.0D0

  INTEGER :: I

  DOUBLE PRECISION, EXTERNAL :: DLARND
  EXTERNAL :: SEEDOK

  CALL SEEDOK(ISEED, I)

  IF (N .LT. 0) THEN
    INFO = -1
  ELSE IF (I .NE. 0) THEN
    INFO = -2
  ELSE IF ((IDIST .LT. 1) .OR. (IDIST .GT. 3)) THEN
    INFO = -3
  ELSE IF (EPS .LT. ZERO) THEN
    INFO = -4
  ELSE IF (SCAL .EQ. ZERO) THEN
    INFO = -5
  ELSE
    INFO = 0
  END IF
  IF (INFO .NE. 0) RETURN

  NPOS = 0
  I = 1

  DO WHILE (I .LE. N)
    LAM(I) = DLARND(IDIST, ISEED)
    IF (ABS(LAM(I)) .GT. EPS) THEN
      IF (LAM(I) .GT. ZERO) NPOS = NPOS + 1
      I = I + 1
    END IF
  END DO

  IF (SCAL .NE. ONE) THEN
    DO I = 1, N
      LAM(I) = LAM(I) * SCAL
    END DO
  END IF
END SUBROUTINE DGENLAM
